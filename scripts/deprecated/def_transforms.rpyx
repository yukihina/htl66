init:
##### Opacity Effects
    transform alphaa:
        alpha 0.5

    transform fade_in:
        alpha 0.0
        linear 1.0 alpha 1.0

    transform fade_out:
        alpha 1.0
        linear 1.0 alpha 0.0
    
    transform neon_flicker:
        alpha 0.0
        linear 0.05 alpha 1.0
        pause 0.5
        linear 0.05 alpha 0.0
        pause 0.1
        linear 0.05 alpha 1.0
        pause 0.5
        linear 0.05 alpha 0.0
        pause 0.1
        linear 0.05 alpha 1.0
        pause 1.0
        linear 0.05 alpha 0.0
        pause 0.2
        linear 0.05 alpha 1.0
        pause 0.7
        repeat


##### In-game menu
    transform igmappear_nowait:
        alpha 0.0
        pause 0.05
        ease 0.5 alpha 1.0
        on show:
            alpha 0.0
            pause 0.05
            ease 0.5 alpha 1.0
        on hide:
            alpha 1.0
            ease 0.5 alpha 0.0

    transform igmappear_bg:
        alpha 0.0
        pause 0.35
        ease 0.5 alpha 1.0
        on show:
            alpha 0.0
            pause 0.35
            ease 0.5 alpha 1.0
        on hide:
            alpha 1.0
            ease 0.5 alpha 0.0

    transform igmappear:
        alpha 0.0
        pause 0.5
        ease 0.5 alpha 1.0
        on show:
            alpha 0.0
            pause 0.5
            ease 0.5 alpha 1.0
        on hide:
            alpha 1.0
            ease 0.5 alpha 0.0

    transform igmappear_fg:
        alpha 0.0
        pause 0.65
        ease 0.5 alpha 1.0
        on show:
            alpha 0.0
            pause 0.65
            ease 0.5 alpha 1.0
        on hide:
            alpha 1.0
            ease 0.5 alpha 0.0
        
    transform igmappear_fg2:
        alpha 0.0
        pause 0.9
        ease 0.5 alpha 1.0
        on show:
            alpha 0.0
            pause 0.9
            ease 0.5 alpha 1.0
        on hide:
            alpha 1.0
            ease 0.5 alpha 0.0

#### Movement Effects
    transform vpunch_effect(time=0.3, offset=10, pause=1):
        ease time/4 yoffset -offset
        ease time/4 yoffset offset
        ease time/4 yoffset -offset
        ease time/4 yoffset 0
        pause pause
        repeat

    transform hpunch_effect(time=0.3, offset=10, pause=1):
        ease time/4 xoffset -offset
        ease time/4 xoffset offset
        ease time/4 xoffset -offset
        ease time/4 xoffset 0
        pause pause
        repeat

    transform bounce:
        # This transform creates a bouncing effect by animating the zoom, alpha, and x/y zoom values.
        subpixel True
        xalign 0.5
        yalign 0.5
        zoom 0.1
        alpha 0.0
        ease 0.5 zoom 1.0 alpha 1.0
        ease 0.2 xzoom 1.2 yzoom 0.8
        ease 0.2 xzoom 0.9 yzoom 1.1
        ease 0.2 xzoom 1.1 yzoom 0.9
        ease 0.2 xzoom 0.95 yzoom 1.05
        ease 0.2 xzoom 1.05 yzoom 0.95
        ease 0.2 xzoom 1.0 yzoom 1.0

    transform dizzyness:
        # This transform creates a dizzy effect by moving the image in a circular pattern.
        subpixel True
        zoom 1.05
        anchor(.5, .5)
        pos(.5, .5)
        on show:
            parallel:
                linear 2.5 xalign 0.6 yalign 0.5
                linear 2.5 xalign 0.6 yalign 0.6
                linear 2.5 xalign 0.4  yalign 0.6
                linear 2.5 xalign 0.4  yalign 0.4
            repeat
    
    transform heartbeat(a=0.975, b=1.0, x=0.8):
    # This transform creates a pulsating effect by zooming in and out repeatedly with slight opacity changes.
        alpha x
        subpixel True
        zoom a
        ease 1 zoom b
        ease 1 zoom a
        repeat

    transform sway:
        # This transform creates a swaying effect by shifting the image horizontally.
        linear 0.3 xoffset 20
        linear 0.3 xoffset -20
        linear 0.3 xoffset 0
        choice:
            pause 1.085
        choice:
            pause 2.8
        choice:
            pause 3.5
        repeat

    transform concentrate:
        # This transform creates a pulsating effect by zooming in and out repeatedly.
        subpixel True
        truecenter
        zoom 1.0
        ease 10 zoom 1.05
        ease 10 zoom 1.0
        repeat

#### Zoom Effects
    transform zoomin:
        subpixel True
        truecenter
        zoom 1.0
        easein 0.5 zoom 1.05
        ease 1.0 zoom 1.2
        ease 2.0 zoom 1.15
        easein 6.5 zoom 1.2
        ease 2.0 zoom 1.0

    transform zoomout:
        # This transform causes the camera to zoom out the image.
        subpixel True
        truecenter
        zoom 1.2
        ease 10 zoom 1.0

    transform camera_zoom:
        subpixel True
        truecenter
        zoom 1.0
        ease 2.0 zoom 1.1
        pause 2.0
        ease 2.0 zoom 1.0

    #### Dramatic Zoom
    transform dramatic_zoom:
        subpixel True
        align (0.5, 0.5)
        zoom 1.0
        ease 1.5 zoom 1.3

    #### Ken Burns Variations
    transform ken_burns_left_to_right:
        subpixel True
        align (0.0, 0.5)
        zoom 1.05
        linear 10.0 align (1.0, 0.5)

    transform ken_burns_right_to_left:
        subpixel True
        align (1.0, 0.5)
        zoom 1.05
        linear 10.0 align (0.0, 0.5)

    transform ken_burns_top_to_bottom:
        subpixel True
        align (0.5, 0.0)
        zoom 1.05
        linear 10.0 align (0.5, 1.0)

    transform ken_burns_bottom_to_top:
        subpixel True
        align (0.5, 1.0)
        zoom 1.05
        linear 10.0 align (0.5, 0.0)

    transform ken_burns_corner_to_corner:
        subpixel True
        align (0.0, 0.0)
        zoom 1.1
        linear 15.0 align (1.0, 1.0)

    transform ken_burns_corner_to_corner2:
        subpixel True
        align (1.0, 0.0)
        zoom 1.1
        linear 15.0 align (0.0, 1.0)

    #### Subtle Zoom Effects
    transform subtle_zoom_in:
        subpixel True
        align (0.5, 0.5)
        zoom 1.0
        ease 5.0 zoom 1.1

    transform subtle_zoom_out:
        subpixel True
        align (0.5, 0.5)
        zoom 1.1
        ease 5.0 zoom 1.0

    #### Impact Shake
    transform impact_shake:
        subpixel True
        align (0.5, 0.5)
        xoffset 0
        ease 0.02 xoffset 20
        ease 0.02 xoffset -15
        ease 0.02 xoffset 10
        ease 0.02 xoffset -5
        ease 0.02 xoffset 0
        repeat 3

    #### Focus Pulse
    transform focus_pulse:
        subpixel True
        align (0.5, 0.5)
        blur 0
        ease 2.0 blur 10
        ease 2.0 blur 0
        repeat

    #### Combined Effects
    transform dramatic_focus:
        subpixel True
        align (0.5, 0.5)
        zoom 1.0
        parallel:
            ease 3.0 zoom 1.15
        parallel:
            blur 0
            ease 1.5 blur 10
            ease 1.5 blur 0

    transform slow_reveal:
        subpixel True
        align (0.0, 0.5)
        zoom 1.1
        parallel:
            linear 15.0 align (1.0, 0.5)
        parallel:
            ease 5.0 zoom 1.0
            
    #### Focus Shift
    transform focus_shift:
        subpixel True
        align (0.5, 0.5)
        blur 10
        pause 0.5
        linear 1.0 blur 0

    #### Combined Effects

    # Dramatic Realization
    transform dramatic_realization:
        subpixel True
        align (0.5, 0.5)
        zoom 1.0
        blur 0
        parallel:
            ease 1.5 zoom 1.3
            ease 5.0 zoom 1.0
        parallel:
            linear 0.5 blur 10
            linear 1.0 blur 0

    # Action Sequence
    transform action_sequence:
        subpixel True
        align (0.5, 0.5)
        zoom 1.0
        rotate 0
        parallel:
            ease 0.5 zoom 1.2
        parallel:
            ease 0.5 rotate -5
        parallel:
            ease 0.02 xoffset 20
            ease 0.02 xoffset -15
            ease 0.02 xoffset 10
            ease 0.02 xoffset -5
            ease 0.02 xoffset 0
            repeat 2

    # Emotional Flashback
    transform emotional_flashback:
        subpixel True
        align (0.0, 0.0)
        zoom 1.1
        blur 0
        parallel:
            linear 10.0 align (1.0, 1.0)
        parallel:
            linear 3.0 blur 5
            linear 3.0 blur 0
            repeat
            
    transform photo_effect:
        subpixel True
        align (0.5, 0.5)
        zoom 1.1  # Start with a slightly larger zoom to prevent edge visibility
        
        # Slight camera movement before the shot
        ease 0.2 xoffset 5 yoffset -5
        ease 0.2 xoffset -3 yoffset 3
        
        # Camera "click" movement
        ease 0.05 zoom 1.09  # Zoom out slightly, but not below 1.0
        ease 0.05 zoom 1.1
        
        # Post-shot stabilization
        ease 0.3 xoffset 0 yoffset 0

    # Flash overlay
    image photo_flash:
        Solid("#FFFFFF")
        alpha 0.0
        linear 0.1 alpha 0.8
        linear 0.1 alpha 0.0


    transform subtle_breathing:
        subpixel True
        align (0.5, 0.5)
        zoom 1.1
        ease 2.0 yoffset -10
        ease 2.0 yoffset 0
        repeat



#### Special Effects
    transform eyaculation_flash:
        alpha 0.0
        linear 0.2 alpha 0.2
        linear 0.2 alpha 0.4
        linear 0.2 alpha 0.6
        linear 0.2 alpha 0.8
        linear 0.1 alpha 1.0
        linear 0.1 alpha 0.8
        linear 0.1 alpha 1.0
        linear 0.1 alpha 0.8
        linear 0.1 alpha 1.0
        linear 0.1 alpha 0.8
        linear 0.1 alpha 1.0
        linear 0.2 alpha 0.6
        linear 0.2 alpha 0.4
        linear 0.2 alpha 0.2
        linear 0.2 alpha 0.0
    # show PICTURE with flash
    # show white zorder 1.0 at eyaculation_flash

    transform pov_die:
        alpha 0.0
        easein 1.0 alpha 1.0
        easeout 1.0 alpha 0.0
        easein 1.0 alpha 0.8
        easeout 1.0 alpha 0.0
        easein 1.0 alpha 0.6
        easeout 1.0 alpha 0.0
        easein 1.0 alpha 0.4
        easeout 1.0 alpha 0.0
        repeat

    transform slow_pan:
        yalign 0.0
        linear 20.0 yalign 1.0
        pause 2.0
        linear 20.0 yalign 0.0
        repeat